apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'

group = 'expo.modules.alipush'
version = '0.1.0'

def enableXiaoMiPush = findProperty("alipush.third.xiaomi.enabled") ?: false
def enableHuaweiPush = findProperty("alipush.third.huawei.enabled") ?: false

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useExpoPublishing()

// If you want to use the managed Android SDK versions from expo-modules-core, set this to true.
// The Android SDK versions will be bumped from time to time in SDK releases and may introduce breaking changes in your module code.
// Most of the time, you may like to manage the Android SDK versions yourself.
def useManagedAndroidSdkVersions = false
if (useManagedAndroidSdkVersions) {
  useDefaultAndroidSdkVersions()
} else {
  buildscript {
    // Simple helper that allows the root project to override versions declared by this library.
    ext.safeExtGet = { prop, fallback ->
      rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
    }
  }
  project.android {
    compileSdkVersion safeExtGet("compileSdkVersion", 34)
    defaultConfig {
      minSdkVersion safeExtGet("minSdkVersion", 21)
      targetSdkVersion safeExtGet("targetSdkVersion", 34)
    }
  }
}

android {
  namespace "expo.modules.alipush"
  defaultConfig {
    versionCode 1
    versionName "0.1.0"

    consumerProguardFiles 'proguard-rules.pro'

    buildConfigField("boolean", "XIAOMI_PUSH_ENABLED", enableXiaoMiPush.toString())
    buildConfigField("boolean", "HUAWEI_PUSH_ENABLED", enableHuaweiPush.toString())
  }
  lintOptions {
    abortOnError false
  }
}

rootProject.allprojects {
  repositories {
    maven {url 'https://developer.huawei.com/repo/'}
    maven {
      url 'http://maven.aliyun.com/nexus/content/repositories/releases/'
      name 'aliyun'
      allowInsecureProtocol = true
    }
  }
}

dependencies {
  implementation project(':expo-notifications')

  api 'com.aliyun.ams:alicloud-android-push:3.8.8.1'

  api 'com.aliyun.ams:alicloud-android-third-push:3.8.8'
  api 'com.aliyun.ams:alicloud-android-third-push-xiaomi:3.8.8'
  api 'com.aliyun.ams:alicloud-android-third-push-huawei:3.8.8'
}
